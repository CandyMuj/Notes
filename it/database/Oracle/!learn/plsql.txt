一、pl/sql块由三个部分构成:定义部分、执行部分、例外处理部分.

declare 
--定义部分（定义常量、变量、游标、例外、复杂数据类型）
begin 
--执行部分（要执行的pl/sql语句和sql语句）
exception
--例外处理部分（处理运行各种错误）
end;

例1：输出Hello,Word!

set serveroutput on
--打开输出选项
begin
dbms_output.put_line('Hello,Word!');
--dbms_output是oracle的包，put_line过程，功能就是输出内容。
end;

例2:根据用户输入的雇员编号，显示该雇员的名字 
declare
v_ename varchar2(10);
begin
select ename into v_ename from emp where empno=&no;
--&从控制台输入变量
dbms_output.put_line('员工的姓名是：'||v_ename);
end;

？思考如何根据用户输入的雇员编号，显示该雇员的名字，薪水，部门号
declare
v_ename varchar2(10);
v_sal number;
v_deptno number;
begin
select ename, sal, deptno into v_ename, v_sal, v_deptno from emp where empno=&no;
dbms_output.put_line('员工的姓名是：'||v_ename||' 员工的薪水是：'||v_sal||' 员工的部门是：'||v_deptno);
end;


例3：如果输入雇员编号错误，例外处理(no_data_found)

declare
v_ename varchar2(10);
v_sal number;
v_deptno number;
begin
select ename, sal, deptno into v_ename, v_sal, v_deptno from emp where empno=&no;
dbms_output.put_line('员工的姓名是：'||v_ename||' 员工的薪水是：'||v_sal||' 员工的部门是：'||v_deptno);
--异常处理
exception
when no_data_found then
dbms_output.put_line('您输入的员工编号不存在！');
end;

pl/sql块课件练习：根据雇员编号，查出姓名，所在部门以及雇员领导的姓名。
declare
v_ename varchar2(20);
v_dname varchar2(20);
v_mgrname varchar2(20);
begin
select e1.ename, d.dname, e2.ename into v_ename, v_dname, v_mgrname from emp e1, emp e2, dept d where e1.mgr=e2.empno and e1.deptno=d.deptno and e1.empno=&雇员号;
dbms_output.put_line('员工的姓名是：'||v_ename||' 员工的部门是：'||v_dname||' 员工的上级是：'||v_mgrname);
exception
when no_data_found then
dbms_output.put_line('您输入的员工编号不存在！');
end;

二、在编写pl/sql程序时，可以定义变量和常量；在pl/sql程序中包括有:
1、标量类型(scalar)
    
 （1）标量定义的案例
      ①定义一个变长字符串
       v_ename	varchar2(10);

      ②定义一个小数 范围 -9999.99～9999.99
       v_sal   number(6,2);

      ③定义一个小数并给一个初始植为5.4 :=是pl/sql的赋值号
       v_sal2  number(6,2):=5.4

      ④定义一个日期类型的数据 
       v_hiredate  date; 
 
      ⑤定义一个布尔变量（真和假），不能为空，初始植为false
      v_valid  boolean  not null  default false;

  （2）使用%type类型  
 
      标识符名 表名.列名%type;
       
declare
v_ename emp.ename%type;
v_sal emp.sal%type;
v_deptno dept.deptno%type;
begin
select ename, sal, deptno into v_ename, v_sal, v_deptno from emp where empno=&no;
dbms_output.put_line('员工的姓名是：'||v_ename||' 员工的薪水是：'||v_sal||' 员工的部门是：'||v_deptno);
end;
  
   （3）%rowtype

   表示该类型为行数据类型，存储的是一行数据，一行数据里可以有多列，类似于表里的一行数据，也可以是游标里的一行数据，如：
   vs_row1  表%rowtype;
   vs_row2  游标%rowtype;
   
   例如：编写一个程序块，接受用户输入一个部门号，从dept表中显示该部门的名称与所在位置。

   方法一：（传统方法） 
declare  
v_loc dept.dname%type; 
v_dname dept.dname%type; 
v_deptno dept.deptno%type; 
begin 
v_deptno :=&部门编号;
select loc,dname into v_loc,v_dname from dept where deptno=v_deptno;
dbms_output.put_line('员工所在地是：'||v_loc||'；部门名称是：'||v_dname); 
exception  
when no_data_found then 
dbms_output.put_line('您输入的部门编号不存在，请从新输入，谢谢'); 
end;

   方法二：（使用%rowtype）    
declare     
v_dept dept%rowtype;    
begin     
select * into v_dept from dept where deptno=&部门号;    
dbms_output.put_line(v_dept.dname||'--'||v_dept.loc);    
end; 

2、复合类型(composite)

(1) 复合类型-pl/sql记录类似与高级语言中的结构体,需要注意的是,当引用pl/sql记录成员时,必须要加记录变量作为前缀(记录变量.记录成员)
如下：

declare
type emp_record_type is record(
name emp.ename%type,
salary emp.sal%type,
title emp.job%type);
sp_record emp_record_type;
begin
select ename,sal,job into sp_record from emp where empno=&雇员号;
dbms_output.put_line('员工名:'||sp_record.name);
end;

(2) 复合类型-pl/sql表相当于高级语言中的数组.但是需要注意的是在高级语言中数组的下标不能为负数,而pl/sql是可以为负数的,并且表元素的下标没有限制.
实例如下:

declare
type sp_table_type is table of emp.ename%type
index by binary_integer;--表示下标是整数
sp_table sp_table_type;--定义一个变量:sp_table :类型
begin
select ename into sp_table(-1) from emp where empno=7788;
dbms_output.put_line('员工名:'|| sp_table(-1));
end;
说明：　sp_table_type　   是pl/sql表类型
	emp.ename%type    指定了表的元素的类型和长度
	sp_table          为pl/sql表变量
	sp_table(0)       则表示下标为0的元素



3、参照类型(reference)

(1)参照变量-介绍
	
参照变量是指用于存放数值指针的变量.通过使用参照变量，可以使得应用程序共享相同对象，从而降低占用的空间。在编写pl/sql程序时，可以使用游标变量(ref cursor) 和对象类型变量,(ref obj_type)两种参照变量类型

declare
--定义游标类型sp_emp_cursor
type sp_emp_cursor is ref cursor;
--定义一个游标变量
test_cursor sp_emp_cursor;
--定义变量
v_ename emp.ename%type;
v_sal emp.sal%type;
begin
--执行
--把test_cursor和一个select结合
open test_cursor for select ename, sal from emp where deptno=&部门名;
--循环取出
loop
fetch test_cursor into v_ename, v_sal;
--判断是否test_cursor为空
exit when test_cursor%notfound;
dbms_output.put_line('员工名:'|| v_ename||' ,工资是：'||v_sal);
end loop;
--关闭游标
close test_cursor;
end;


pl/sql控制语句(循环，分支，控制)
一、循环
1、loop循环
 
格式:
Loop
  循环的语句；
exit when 终止条件;
  循环的条件必须更改;
end loop;

例：输出数字1-10

declare
cou number;
begin
--给一个初始值
cou:=1;
loop 
 dbms_output.put_line('cou='||cou);
 exit when cou>9;
 cou:=cou+1;
end loop;
end;

declare
cou number;
begin
--给一个初始值
cou:=1;
loop 
 dbms_output.put_line('cou='||cou);
 cou:=cou+1;
 exit when cou>10;
end loop;
end;
--先执行一次再进行判断！


2、while循环

格式：
while(判断循环的条件) loop
 循环语句;
 循环条件改变;
end loop;

例：输出数字1-10
declare
cou number;
begin
cou:=1;
while cou<11 loop
 dbms_output.put_line('cou='||cou);
 cou:=cou+1;
end loop;
end;
--先判断，条件满足再执行


3、for循环

格式:
for 变量名 in 变量的初始值..结束值 loop
  循环语句;
end loop;


例：输出数字1-10
declare
cou number;
begin
 for cou in 1..10 loop
  dbms_output.put_line('cou='||cou);
 end loop;
end;

二、分支
1、if...then


例：当数字大于10时,输出这个数字
方法一：
declare
cou number;
begin
 cou:=12;
 if cou>10 then
   dbms_output.put_line('cou='||cou);
 end if;
end;

方法二：
declare
cou number:=12;
begin
 if cou>10 then
   dbms_output.put_line('cou='||cou);
 end if;
end;

错误的写法：
declare
cou number
cou:=12;--error
begin
 if cou>10 then
   dbms_output.put_line('cou='||cou);
 end if;
end;

2、if...then...else如果if满足，则执行，否则执行else

例：输入一个数字，当数字大于10时,输出这个数字,当不满足时输出'条件不满足'
declare
cou number;
begin
 cou:=&no;
 if cou>10 then
   dbms_output.put_line('cou='||cou);
 else
   dbms_output.put_line('条件不满足');
 end if;
end;


3、if...then...elsif...then

例：输入一个数字，当数字大于10时,输出这个数字；当数字<5时,输出'值小于5'；当在5-10之间时，输出'输出的数字是5-10之间的数'。

declare
cou number;
begin
  cou:=&no;
  if cou>10 then
     dbms_output.put_line('cou='||cou);
  elsif cou<5 then
     dbms_output.put_line('值小于5');
  else
     dbms_output.put_line('输出的数字是5-10之间的数');
  end if;
end;

三、goto无条件跳转语句
 
declare
  i number:=1;
begin
  loop 
   dbms_output.put_line('输出i='||i);
   if i=10 then
   goto end_loop;
   end if;
   i:=i+1;
  end loop;
  <<end_loop>>
  dbms_output.put_line('循环结束');
end;

四、null
null语句不会执行任何操作，直接将控制传递到下一条语句。
declare
v_sal emp.sal%type;
v_ename emp.ename%type;
begin
 select ename, sal into v_ename, v_sal from emp where empno=&no;
if v_sal < 3000 then
  update emp set comm = sal * 0.1 where ename = v_ename;
else
  null;
end if;
end;

――――过程
1、概念:
    用于执行特定的操作，当建立过程时，既可指定输入参数（in）,也可指定输出参数（out），通过在过程中使用输入参数，可以将数据传递到执行部分；通过使用输出参数，可以讲执行部分的数据传递到应用环境。

2、语法：
create [or replace] procedure <porcedure_name>  [(参数名参数类型以及描述,....)] --注意（）内表明参数的类型，不需要具体大小。
is  [变量声明] 
begin  [过程处理];
exception  
when 异常名 then end;  
注意:参数中默认是按值传递.是in方式.也可以是out和in out方式.这些特点和函数一样. 

3、调用的方法：
 exec sp_name;
 call sp_name;
例1：输入雇员名和新工资，修改雇员原来的工资。

 create or replace procedure sp_pro1(Nsal number,Nname varchar2) as 
 begin
   update emp set sal=Nsal where ename=Nname;
 end;
 
 exec sp_pro1(3000,'SMITH');
 call sp_pro1(3001,'SMITH');

例2：建一个过程，根据部门号输出部门员工所在地和部门名称，如果部门号不存在，显示‘您输入的部门编号不存在，请从新输入，谢谢！’

create or replace procedure sp_pro2(v_deptno number) is
v_loc dept.dname%type;
v_dname dept.dname%type;
begin 
select loc,dname into v_loc,v_dname from dept where deptno=v_deptno;
dbms_output.put_line('员工所在地是：'||v_loc||'；部门名称是：'||v_dname); 
exception  
when no_data_found then 
dbms_output.put_line('您输入的部门编号不存在，请从新输入，谢谢'); 
end;

例3：例:建立一个存储过程，在emp表中给按雇员号指定的人员增加工资,如果工资大于2000则增加50，否则如果工资大于1000则增加100，否则增加150。

CREATE OR REPLACE PROCEDURE SP_PRO3(EMPLOYNO EMP.EMPNO%TYPE) IS 
INCREMENT NUMBER; 
SALARY EMP.SAL%TYPE; 
BEGIN  
SELECT SAL INTO SALARY FROM EMP  WHERE EMPNO=EMPLOYNO; 
IF SALARY>=2000 THEN INCREMENT:=50;  
ELSIF SALARY>=1000 THEN INCREMENT:=100; 
ELSE  INCREMENT:=150; 
END IF;  
UPDATE EMP  SET SAL=SAL+INCREMENT WHERE EMPNO=EMPLOYNO; 
END; 


――――函数
1、概念
   用于返回特定的数据，当建立函数时，在函数头部必须包含return子句。而在函数体内必须包含return语句返回的数据。
   使用CREATE?FUNCTION来建立函数。

2、语法：
CREATE?[OR?REPLACE]?FUNCTION?function_name?
(arg1?[?{?IN?|?OUT?|?IN?OUT?}]?type1?[DEFAULT?value1],?[argn?[?{?IN?|?OUT?|?IN?OUT?}]?typen?[DEFAULT?valuen]])??
[?AUTHID?DEFINER?|?CURRENT_USER?]?RETURN?return_type???
IS?|?AS????<类型.变量的声明部分>?
BEGIN?????执行部分
RETURN?expression?EXCEPTION?????异常处理部分?
END?function_name;

3、

例1：--获取某部门的工资总和 
CREATE OR REPLACE FUNCTION get_salary(Dept_no NUMBER)   RETURN NUMBER  IS    
V_sum NUMBER; 
BEGIN    
select  sum(sal) into V_sum from emp where deptno=dept_no;   
RETURN v_sum; 
EXCEPTION     
WHEN NO_DATA_FOUND THEN         
DBMS_OUTPUT.PUT_LINE('你需要的数据不存在!');    
WHEN OTHERS THEN         
DBMS_OUTPUT.PUT_LINE(SQLCODE||'---'||SQLERRM); 
END get_salary;

SQL> select get_salary(10) from dual;
//或者
SQL> var income number;
SQL> call get_salary(10)into:income;
SQL> print income;


--创建一个函数，输入任意一个值，求该值得累加和

create or replace function N_sum(a number) return number
is
b number :=0;
c number;
begin
for c in 0..a loop
  b := b + c;
  end loop;
  return b;
end N_sum;
/

^程和函刀家跃g後的形式存放在熘校函悼梢]有狄部梢杂卸K有一返回值。^程有零或多担]有返回值。函岛瓦^程都可以通^盗斜斫邮栈蚍祷亓或多值，函岛瓦^程的主要^e不在於返回值，而在於他的{用方式。^程是作橐立绦姓Z句{用的：

――――包
1、概念
   包用于在逻辑上组合过程和函数,它由包规范和包体两部分组成。
2、①使用create package 命令来创建包:
     create or replace package sp_package is
     procedure sp_pro3 (employno emp.empno%type);
     function  get_salary(Dept_no number) return number;
     end;

  ②使用create package body 建立包体:
     
create or replace package body sp_package is
PROCEDURE SP_PRO3(EMPLOYNO EMP.EMPNO%TYPE) IS 
INCREMENT NUMBER; 
SALARY EMP.SAL%TYPE; 
BEGIN  
SELECT SAL INTO SALARY FROM EMP  WHERE EMPNO=EMPLOYNO; 
IF SALARY>=2000 THEN INCREMENT:=50;  
ELSIF SALARY>=1000 THEN INCREMENT:=100; 
ELSE  INCREMENT:=150; 
END IF;  
UPDATE EMP  SET SAL=SAL+INCREMENT WHERE EMPNO=EMPLOYNO; 
END; 
FUNCTION get_salary(Dept_no NUMBER)   RETURN NUMBER  IS    
V_sum NUMBER; 
BEGIN    
select  sum(sal) into V_sum from emp where deptno=dept_no;   
RETURN v_sum; 
EXCEPTION     
WHEN NO_DATA_FOUND THEN         
DBMS_OUTPUT.PUT_LINE('你需要的数据不存在!');    
WHEN OTHERS THEN         
DBMS_OUTPUT.PUT_LINE(SQLCODE||'---'||SQLERRM); 
END get_salary;
end;

  ③如何调用包的过程或是函数

     exec sp_package.sp_pro3(7902);
或者 call sp_package.sp_pro3(7902);
 
――――游标
1、概念：
游标是一种PL/SQL控制结构：可以对SQL语句的处理进行显示控制，便于对表的行数据逐条进行处理
游标应不是一个数据库对象，只是存留在内存中。
2、操作步骤
  ◆ 声明游标
  ◆ 打开游标
  ◆ 取出结果，此时的结果取出的是一行数据
  ◆ 关闭游标

3、使用for循环操作游标
例1：输入部门号，显示全部雇员编号和姓名。
declare
--声明游标
cursor mycur is select * from emp where deptno=&no;
v_emp emp%rowtype;
cou number;
begin
for v_emp in mycur loop
 cou:=mycur%rowcount;--rowcount对游标操作的行数进行记录
 dbms_output.put_line(cou||'雇员编号'||v_emp.empno||'雇员姓名'||v_emp.ename);
 end loop;
end;

另外的写法:
declare
--声明游标
cursor mycur is select * from emp where deptno=10;
v_emp emp%rowtype;
cou number;
begin
open mycur;
fetch mycur into v_emp;
--判断此行是否有数据发现
while(mycur%found) loop
  cou:=mycur%rowcount;--rowcount对游标操作的行数进行记录
  dbms_output.put_line(cou||'雇员编号'||v_emp.empno||'雇员姓名'||v_emp.ename);
  --修改游标，继续向下
  fetch mycur into v_emp;
end loop;
end;

--输出前五行记录
create or replace procedure pro_1
is
cursor mycur is select * from emp;
v_emp emp%rowtype;
cou number :=0;
begin
open mycur;
fetch mycur into v_emp;

while(mycur%found) loop
   cou := cou + 1;
   if cou <= 5 then 
      dbms_output.put_line(cou||'雇员编号'||v_emp.empno||'雇员姓名'||v_emp.ename);
   end if;
   fetch mycur into v_emp;
end loop;
end;

declare     
cursor v_cursor is select * from emp;    
v_count number :=1;    
begin      
for v_emp in v_cursor loop          
dbms_output.put_line(v_emp.ename);         
v_count := v_count+1;         
exit when v_count>5;        
end loop;       
end; 


declare
cursor mycur is select * from emp;
cou number;
v_emp emp%rowtype;
begin
  for v_emp in mycur loop
  cou:=mycur%rowcount;
  if cou > 2 and cou < 6 then
  dbms_output.put_line(v_emp.ename);
  end if;
  end loop; 
end;

课间作业：一次性上涨全部雇员的工资。根据所在的部门涨工资：
―10部门上涨10%
―20部门上涨20%
―30部门上涨30%
―所在部门的上涨工资，最大不能超过5000，超过工资只为5000 

declare
cursor mycur is select * from emp;
v_emp emp%rowtype;
v_sal emp.sal%type;
begin
for v_emp in mycur loop
   if v_emp.deptno=10 then
      v_sal:=v_emp.sal * 1.1;
   elsif v_emp.deptno=20 then
      v_sal:=v_emp.sal * 1.2;   
   elsif v_emp.deptno=30 then
      v_sal:=v_emp.sal * 1.3;
   end if;
   if v_sal>5000 then 
      v_sal:=5000;
   end if;
  update emp set sal=v_sal where empno=v_emp.empno;
end loop;
end;


-----------------------------------------------------------------------------------
练习：
1.某公司要根据雇员的职位来加薪，公司决定按下列加薪结构处理：               
---------------------
Designation    Raise                            
Clerk          500               
Salesman       1000                
Analyst        1500               
Otherwise      2000  
---------------------
编写一个程序块,接受一个雇员姓名，从emp表中实现上述加薪处理。

2. 输入一个雇员的编号，如果其工资高于3500，则显示高工资，工资大于2000，则显示中等工资，小于2000则显示低工资.

3. 输入一个雇员编号，根据他所在的部门涨工资，规则：
-------------------------------------------------------
10部门上涨10%
20部门上涨20%
30部门上涨30%
所在部门的上涨工资，最大不能超过5000，超过工资只为5000 
-------------------------------------------------------
4、向emp表中添加10个雇员编号（8901-8910）

5.编写一个程序块，将emp表中雇员名全部显示出来。    

6.编写一个程序块，将emp表中前5人的名字显示出来。  

7.编写一个程序块，接受一个雇员名，从emp表中显示该雇员的工作岗位与薪水，若输入的雇员名不存在，显示“该雇员不存在”信息。

8.接受两个数相除并且显示结果，如果除数为0，则显示消息“除数不能为0”。 

--------------------------------------------------------------------------------------------
1、
declare       
v_emp emp%rowtype;  
begin        
select * into v_emp from emp where ename='&name';       
if v_emp.job='CLERK' then           
  update emp set sal=sal+500 where empno=v_emp.empno;       
  elsif v_emp.job='SALESMAN' then           
  update emp set sal=sal+1000 where empno=v_emp.empno;        
  elsif v_emp.job='ANALYST' then           
  update emp set sal=sal+1500 where empno=v_emp.empno;        
  else             
  update emp set sal=sal+2000 where empno=v_emp.empno;   
  end if;       
  end;
-------------------------------------------------------
2、
declare
v_sal emp.sal%type;
begin
 select sal into v_sal from emp where empno=&no;
 if v_sal >3500 then
 dbms_output.put_line('高工资');
 elsif v_sal>2000 then
 dbms_output.put_line('中等工资');  
 else 
 dbms_output.put_line('低工资');  
 end if;
end;
-------------------------------------------------------
3、方法一：
declare
v_sal emp.sal%type;
v_dno emp.deptno%type;
v_empno emp.empno%type;
begin
 v_empno := &no;
 select deptno, sal into v_dno, v_sal from emp where empno=v_empno;
 if v_dno = 10 then
   if v_sal*1.1 > 5000 then
   update emp set sal = 5000 where empno=v_empno;
   else 
   update emp set sal = sal *1.1 where empno=v_empno;
   end if;
 elsif v_dno = 20 then
   if v_sal*1.2 > 5000 then
   update emp set sal = 5000 where empno=v_empno;
   else 
   update emp set sal = sal *1.2 where empno=v_empno;
   end if;
 elsif v_dno = 30 then
   if v_sal*1.3 > 5000 then
   update emp set sal = 5000 where empno=v_empno;
   else 
   update emp set sal = sal *1.3 where empno=v_empno;
   end if;
 else
   null;
 end if;
end;
 方法二：
create or replace procedure sp_pro1(v_empno emp.empno%type) is
v_sal emp.sal%type;
v_dno emp.deptno%type;
begin
  select deptno, sal into v_dno, v_sal from emp where empno=v_empno;
  if v_dno = 10 then
    v_sal := v_sal * 1.1;
  elsif  v_dno = 20 then
    v_sal := v_sal * 1.2;
  elsif  v_dno = 30 then
    v_sal := v_sal * 1.3;
  end if;

  if v_sal > 5000 then
     v_sal := 5000;
  end if;

  update emp set sal = v_sal where empno = v_empno;
end;

-------------------------------------------------------
4、
declare
v_empno emp.empno%type;
begin
  for v_empno in 8901..8910 loop
  insert into emp(empno) values (v_empno);
  end loop;
end;
-------------------------------------------------------
5、
declare     
cursor v_cursor is select * from emp;    
begin      
for v_emp in v_cursor loop          
dbms_output.put_line(v_emp.ename);        
end loop;       
end;   
-------------------------------------------------------
6、
declare     
cursor v_cursor is select * from emp;    
v_count number :=1;    
begin      
for v_emp in v_cursor loop          
dbms_output.put_line(v_emp.ename);         
v_count := v_count+1;         
exit when v_count>5;        
end loop;       
end;  
-------------------------------------------------------
7、 
declare      
v_emp emp%rowtype;    
my_exception Exception;  
begin         
 select * into v_emp from emp where ename='&name';    
 raise my_exception; --抛出用户定义异常
 exception           
 when no_data_found then                
 dbms_output.put_line('该雇员不存在!');          
 when others then            
 dbms_output.put_line(v_emp.job||'---'||v_emp.sal);  
end; 
-------------------------------------------------------
8、
declare    
a number;   
b number;   
begin          
a:=&被除数;         
b:=&除数;          
dbms_output.put_line(a||'除以'||b||'='||a/b);        
exception  
when zero_divide then     
dbms_output.put_line('除数不能为0!');   
end;
----------------------------------------------------------------------------------------------


1、触发器
    当用户登陆/退出或操作某个数据对象，或进行DDL（建表，建库，建视图等等）引起某个存储过程的值，把这个隐含被调用的过程称为触发器。

2、触发器的分类
dml（insert, delete,update）触发器(重点)
ddl(create,drop,atler)触发器
系统事件触发器(与系统相关的触发器，比如用户登录，退出，启动数据库，关闭数据库)；



―――――――――――――――――――――――――――――――――――――――――
一、dml 触发器  表，12个触发器，32KB
--语法：

create [or replace] trigger trigger_name
{before|after}
{insert|delete|update[of column...]}
on[schema]table_name
[for each row]--行级触发，如果不写默认就是语句触发
[when condition]
begin
trigger_body;
end;

(1)在emp添加一条数据的时候，提示‘添加了一条数据’

create or replace trigger tri1
after insert on
scott.emp
begin
dbms_output.put_line('添加了一条数据');
end;

(2)在emp修改了多条数据的时候，提示‘多次修改了数据’

create or replace trigger tri2
after update on
scott.emp
begin
dbms_output.put_line('修改了一条数据');
end;

执行操作：
SQL>update emp set sal=sal+10;
修改了一条记录
14 rows update

发现只提示了一条记录

原因是默认的语句级触发器，认为所有的是一句触发操作，修改成行级触发操作（for each row）
create or replace trigger tri2
after update on
scott.emp
for each row--行级触发器
begin
dbms_output.put_line('修改了一条数据');
end;

执行操作：
 
SQL> update emp set sal=sal+10;
 
修改了一条数据
修改了一条数据
修改了一条数据
修改了一条数据
修改了一条数据
修改了一条数据
修改了一条数据
修改了一条数据
修改了一条数据
修改了一条数据
修改了一条数据
修改了一条数据
修改了一条数据
修改了一条数据
 
14 rows updated

(3)before语句触发器
例如：为了禁止工作人员在休息日改变员工信息，开发人员可以建立before语句触发器，从而实现数据的安全。

create or replace trigger tri3
before delete on
scott.emp
begin
  if to_char(sysdate, 'day') in ('星期日','星期六') then
  dbms_output.put_line('周末不能删除员工！');
  raise_application_error(-20001,'周末不能删除员工！');
  end if;
end;

说明：
--raise_application_error(-20001,'周末不能删除员工！')是oracle提供的一个过程,可以传入两个参数。
--error_number_in【自定义】从-20000到-20999之间，这样就不会与oracle的任何错误代码发生冲突。
--error_msg_in【自定义】长度不能超过2K，否则截取2K。

procedure raise_application_error(error_number_in in number,error_msg_in in varchar2)'


执行操作
SQL> delete from emp where empno=1234;
 
周末不能删除员工！
 
delete from emp where empno=1234
 
ORA-20001: 周末不能删除员工！
ORA-06512: 在 "SCOTT.TRI3", line 4
ORA-04088: 触发器 'SCOTT.TRI3' 执行过程中出错

（4）使用条件谓词,精确提示用户的操作。
   当触发器中同时包含多个触发事件（insert,update,delete）时，为了在触发器代码中区分具体的触发事件，可以使用三个条件
inserting
updating
deleting

使用条件谓词来精确提示用户的操作。
为了禁止工作人员在休息日改变员工信息，开发人员可以建立before触发器，从而实现数据的安全，在给出提示时，明确提示用户是进行的insert,update还是delete的操作。


create or replace trigger tri1
before 
insert or update or delete on
scott.emp
for each row
begin
case
  when inserting then
   if (to_char(sysdate,'day')='星期三') then 
   dbms_output.put_line('不要添加!');
   raise_application_error(-20000,'不要添加!');
   end if;
  
  when updating then
   if (to_char(sysdate,'day')='星期三') then 
   dbms_output.put_line('不要更新!');
   raise_application_error(-20000,'不要更新!');
   end if;
   
  when deleting then
   if (to_char(sysdate,'day')='星期三') then 
   dbms_output.put_line('不要删除!');
   raise_application_error(-20000,'不要删除!');
   end if;
end case;
end;

(5)使用:old和:new
  当触发器被触发时，要使用被插入、更新或删除的记录中的列值，有时要使用操作前、后列的值。
:new 修饰符访问操作完成后列的值
:old 修饰符访问操作完成前列的值

特性   insert    update     delete
old    null       有效	     有效
new    有效	  有效       null

例：在修改emp表雇员的薪水时，显示雇员工资修改前和修改后的值；
    如何确保修改员工的工资不能低于原有工资；

create or replace trigger tri5
before  update on
scott.emp
for each row
begin
  if :new.sal<:old.sal then
  dbms_output.put_line('工资不能低于原来工资');
  raise_application_error(-20005,'工资不能低于原来工资');
  else
  dbms_output.put_line('原来工资是'||:old.sal||'现在的工资'||:new.sal);
  end if;
end;

练习：
--编写一个触发器，保证当用户在删除表emp记录的时候，自动把删除的记录备份到另外一张表emp_bak中。

--创建emp_bak表结构
create table myemp(
id number,
name varchar2(50)
);

create or replace trigger tri6
before delete on
scott.emp
for each row
begin
  insert into myemp(id,name) values (:old.empno, :old.ename);
end;

--编写一个触发器如何控制员工的新工资不能低于原来的工资，同时也不能高出原来工资的20%。
create or replace trigger tri5
before update on
scott.emp
for each row
begin
  if (:new.sal<:old.sal or :new.sal>:old.sal*1.2) then
  dbms_output.put_line('工资不在允许的范围内!');
--阻止执行
  raise_application_error(-20010,'工资不在允许的范围内!');
  end if;
end; 

--编写一个触发器，阻止把小于18岁的用户增加到emp1表中。
-1、创建emp1表
create table emp1(
id number,
name varchar2(20),
birthday date);

-2、创建触发器
create or replace trigger tri7
before insert on
scott.emp1
for each row
begin
  if add_months(:new.birthday,18*12)>sysdate then
  dbms_output.put_line('童工!');
--阻止执行
  raise_application_error(-20011,'童工!');
  end if;
end;

-3、测试：
insert into emp1 values (1,'xiaobai',to_date('2000-1-1','yyyy-mm-dd'));

――――――――――――――――――――――――――――――――――――――――――――――
――――――――――――――――――――――――――――――――――――――――――――

二、系统触发器
   系统触发器主要是针对oracle事件的触发器。如用户登录（logon logoff）数据库的启动或关闭等等。
注意：由系统管理员完成的.
 
――常用的属性函数：
ora_client_ip_address //返回客户端的ip
ora_database_name //返回数据库名
ora_login_user //返回登录用户名
ora_sysevent //返回触发触发器的系统事件名
ora_des_encrypted_password //返回用户des（md5）加密后的密码

--基本语法：
create or replace trigger tri_name
after[before] logon[logoff] on database
begin
--执行语句；
end;

--案例

 1、在sys下先创建一张新表log_table
   create table log_table(
   username varchar2(20), --登录的用户名
   logon_time date, --登录时间
   logoff_time date, --退出时间
   address varchar2(20)  --登录ip
   );

 2、创建登录触发器
 create or replace trigger tri8
 after logon on database
 begin
 insert into log_table(username,logon_time,address) 
 values(ora_login_user,sysdate,ora_client_ip_address);
 end;

 3、创建退出触发器
 create or replace trigger tri9
 before logoff on database
 begin
 insert into log_table(username,logoff_time,address) 
 values(ora_login_user,sysdate,ora_client_ip_address);
 end;

--测试，在sys下测试
select * from log_table;
select username, to_char(logoff_time,'yyyy-mm-dd hh24:mi:ss') from log_table;
_________________________________________________________________________________________

三、ddl触发器
  ddl触发器只要是记录oracle中的create，drop,alter操作。

--语法：
create or replace trigger 触发器名
after ddl on 方案名.schema
begin
--执行语句;
end;

--案例：编写一个触发器，可以记录某个用户进行的ddl操作

 1、创建一张表
create table my_ddl(
event varchar2(64),  --create，alter，drop事件
username varchar2(64), --用户名
ddl_time date          --用户操作的时间
);
 
 2、创建触发器
create or replace trigger tri10
after ddl on scott.schema
begin
 insert into my_ddl values(ora_sysevent,ora_login_user,sysdate);
end;

3、测试
 在scott下创建一张表，在sys登录的下面查看my_ddl表中是否有记录。
_________________________________________________________________________________________

四、管理触发器
 
注意：使用管理员登录

1、禁止触发器

alter trigger 触发器名 disable;

2、激活触发器

alter trigger 触发器名 enable;

3、禁止或激活表的所有触发器

alter table 表名 disable all trigger;
alter table 表名 enable all trigger;

4、删除触发器
drop trigger 触发器名;
